locals {
  api_key_manager          = var.api_key_manager ? ["api_keys_read", "api_keys_write", "api_keys_delete"] : []
  dashboard_manager        = var.dashboard_manager ? ["dashboards_read", "dashboards_write", "teams_read"] : []
  monitors_manager         = var.monitors_manager ? ["monitors_read", "monitors_write", "monitors_downtime", "integrations_read"] : []
  synthetics_manager       = var.synthetics_manager ? ["synthetics_read", "synthetics_write", "synthetics_global_variable_read", "synthetics_global_variable_write", "synthetics_private_location_read"] : []
  users_manager            = var.users_manager ? ["user_access_manage", "user_access_read", "teams_manage"] : []
  org_config_manager       = var.org_config_manager ? ["monitor_config_policy_write", "create_webhooks"] : []
  private_location_manager = var.private_location_manager ? ["synthetics_private_location_write", "synthetics_private_location_read"] : []

  application_key_permissions = concat(
    local.api_key_manager,
    local.dashboard_manager,
    local.monitors_manager,
    local.synthetics_manager,
    local.users_manager,
    local.org_config_manager,
    local.private_location_manager
  )
}

resource "datadog_application_key" "this" {
  name   = "${var.app}-${var.env}-cicd"
  scopes = local.application_key_permissions
}

data "aws_kms_alias" "primary" {
  name = "alias/${var.app}-${var.env}"
}

resource "aws_ssm_parameter" "datadog_application_key" {
  name        = "/${var.app}/${var.env}/datadog/cicd/application_key"
  description = "Managed by CDAP. Application key for ${var.app} in ${var.env} to leverage infrastructure as code."
  tier        = "Intelligent-Tiering"
  value       = datadog_application_key.this.key
  type        = "SecureString"
  key_id      = data.aws_kms_alias.primary.id
}
