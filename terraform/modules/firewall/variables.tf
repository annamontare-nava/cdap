variable "app" {
  description = "The application name (ab2d, bcda, dpc)"
  type        = string
  validation {
    condition     = contains(["ab2d", "bcda", "dpc"], var.app)
    error_message = "Valid value for app is ab2d, bcda, or dpc."
  }
}

variable "env" {
  description = "The application environment (dev, test, sbx, sandbox, prod)"
  type        = string
  validation {
    condition     = contains(["dev", "test", "sbx", "sandbox", "prod"], var.env)
    error_message = "Valid value for env is dev, test, sbx, sandbox, or prod."
  }
}

variable "platform" {
  description = "Object representing the CDAP plaform module."
  type = object({
    kms_alias_primary = object({ target_key_arn = string })
    account_id        = string
  })
}

variable "scope" {
  description = "Firewall scope"
  type        = string
  validation {
    condition     = contains(["CLOUDFRONT", "REGIONAL"], var.scope)
    error_message = "Valid value for scope is CLOUDFRONT or REGIONAL."
  }
}

variable "name" {
  description = "Web ACL name"
  type        = string
}

variable "associated_resource_arn" {
  description = "ARN of the resource to associate the WAF with."
  type        = string
  default     = ""
}

variable "content_type" {
  description = "Content type for firewall responses"
  type        = string
  validation {
    condition     = contains(["APPLICATION_JSON", "TEXT_HTML", "TEXT_PLAIN"], var.content_type)
    error_message = "Valid value for content_type is APPLICATION_JSON, TEXT_HTML, or TEXT_PLAIN."
  }
}

variable "rate_limit" {
  description = "IP rate limit for every 5 minutes"
  type        = number
  default     = 3000
}

variable "ip_sets" {
  description = "IP sets to allow"
  type        = list(string)
  default     = []
}

variable "logging_filter" {
  type = object({
    default_behavior = string
    filters = list(object({
      behavior    = string
      requirement = string
      conditions = list(object({
        action_condition     = optional(string, "")
        label_name_condition = optional(string, "")
      }))
    }))
  })
  description = "Filter to control which requests get logged. Defaults to BLOCK and COUNT only. Override during initial rollout to KEEP all traffic."
  default = {
    default_behavior = "DROP"
    filters = [
      {
        behavior    = "KEEP"
        requirement = "MEETS_ANY"
        conditions = [
          { action_condition = "BLOCK" },
          { action_condition = "COUNT" }
        ]
      }
    ]
  }
}
