locals {
  full_name = "${var.app}-${var.env}-alarm-to-slack"
}

import {
  to = module.sns_to_slack_function.aws_cloudwatch_log_group.function
  id = "/aws/lambda/${local.full_name}"
}

data "aws_ssm_parameters_by_path" "slack_webhook_urls" {
  for_each = toset(var.apps_served)
  path     = "/${each.value}/${var.env}/lambda/slack_webhook_url"
}

module "sns_to_slack_function" {
  source   = "../../modules/function"
  platform = module.platform

  description = "Listens for CloudWatch Alerts and forwards to Slack"

  architecture = "arm64"
  handler      = "lambda_function.lambda_handler"
  runtime      = "python3.13"

  ssm_parameter_paths = flatten([
    for app, data in data.aws_ssm_parameters_by_path.slack_webhook_urls :
    data.path
  ])

  function_role_inline_policies = {
    sqs-trigger = data.aws_iam_policy_document.sqs_trigger.json
  }

  # Point to the local source directory — module handles zip + upload
  source_dir = "${path.module}/lambda_src"

  source_dir_excludes = [
    "test_lambda_function.py",
    "requirements-dev.txt",
    "Makefile",
    ".venv/**",
    "__pycache__/**",
    ".pytest_cache/**",
    "*.pyc",
    "coverage/**",
    "sonar-project.properties",
  ]
  environment_variables = {
    IGNORE_OK = true
    APPS      = join(",", var.apps_served)
    SSM_ENV   = var.env
  }
}

module "sns_to_slack_queue" {
  source = "github.com/CMSgov/cdap/terraform/modules/queue?ref=b177921621c97d02dc4a21f830e4532147aa0749"

  name          = local.full_name
  function_name = module.sns_to_slack_function.name
  app           = var.app
  env           = var.env

  policy_documents = [
    data.aws_iam_policy_document.sqs_queue_policy.json
  ]
}
